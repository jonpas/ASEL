grammar org.jonpas.asel.ASEL hidden(WS, SL_COMMENT)

generate asel "http://www.jonpas.org/asel/ASEL"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

// Base program
Model:
	use+=Use* init=Init? prepare=Prepare? run=Run?;

Init:
	{Init} 'init' '{' code+=InitCodePP* '}';

Prepare:
	{Prepare} 'prepare' '{' code+=RunCodePP* '}';

Run:
	{Run} 'run' '{' code+=RunCodePP* '}';

Use:
	'use' file=ID;

	// Code structure
InitCode:
	InitPin | InitVar | InitClass | PageHandler | InitWiFi | AnyCode;

InitCodePP:
	InitCode | PreProcInit;

RunCode:
	ModePin | VarAssign | {RunCode} WIFI | RETURN | AnyCode;

RunCodePP:
	code=(RunCode | PreProcRun);

AnyCode:
	FuncCall | MethodCall | Logical | Loop;

	// init
InitPin:
	'pin' name=ID '=' value=INT;

InitVar:
	VAR_TYPE name=ID (single=InitSingle? | array=InitArray);

InitSingle:
	'=' NEGATE_OP? value=Param;

InitArray:
	data=ArrayAccess ('=' '{' value+=Param* '}')?;

InitClass:
	'class' name=ID '=' class=ID '(' param+=Param* ')';

PageHandler:
	'pagehandle' name=ID '{' code+=PageHandlerCode* '}';

PageHandlerCode:
	arg=HandleArg | code=(RunCodePP);

HandleArg:
	{HandleArg} 'handle' STRING '{' code+=PageHandlerCode* '}';

RETURN:
	'->' return=STRING;

InitWiFi:
	WIFI ssid=STRING password=STRING pageFile=STRING styleFile=STRING '{' buttonLinks+=WiFiLink* '}';

WiFiLink:
	button=STRING '->' name=[PageHandler];

	// run
ModePin:
	mode=PIN_MODE name=[InitPin];

VarAssign:
	name=ID arrayLength=ArrayAccess? MATH_OP? '=' NEGATE_OP? value=Param;

	// any
FuncCall:
	function=ID '(' param+=Param* ')';

MethodCall:
	class=ID '.' method=ID '(' param+=Param* ')';

PreProcInit:
	PreProcRaw | PreProcIfdefInit;

PreProcRun:
	PreProcRaw | PreProcIfdefRun;

PreProcRaw:
	'raw' raw=STRING;

PreProcIfdefInit:
	'ifdef' cond=Condition '{' code+=InitCode* '}';

PreProcIfdefRun:
	'ifdef' cond=Condition '{' code+=RunCode* '}';

Param:
	{Param} value=VarValue | FuncCall | MethodCall | MathExpr;

	// Expressions
Logical:
	LogicalIf;

LogicalIf:
	'if' cond=Condition '{' code+=(RunCodePP)* '}' else+=LogicalElse*;

LogicalElse:
	{LogicalElse} 'else' ('if' nestedCond=Condition)? '{' code+=(RunCodePP)* '}';

Loop:
	LoopFor | LoopWhile;

LoopFor:
	'for' var=ID 'in' ForType '->' '='? ForType '{' code+=(RunCodePP)* '}';

LoopWhile:
	'while' cond=Condition '{' code+=(RunCodePP)* '}';

Condition:
	NEGATE_OP? ConditionType (LOGICAL_OP NEGATE_OP? cond+=ConditionType)*;

ConditionType:
	{ConditionType} val=VarValue | Comparison | FuncCall | MethodCall;

Comparison:
	val1=VarValue comp=COMPARE_OP val2=VarValue;

MathExpr:
	val1=VarValue subExpr+=MathSubExpr+;

MathSubExpr:
	MATH_OP val2=VarValue;

	// General
VarValue:
	{VarValue} BOOL | {VarValue} INT /* LONG same as INT */ | {VarValue} FLOAT /* DOUBLE same as FLOAT */ | {VarValue} CHAR | {VarValue} STRING | {VarValue} KEYWORD | {VarValue} ID | ID ArrayAccess;

ForType:
	INT /* LONG same as INT */ | FLOAT /* DOUBLE same as FLOAT */ | ID;

ArrayAccess:
	'[' (length=INT | name=ID) ']';

PIN_MODE:
	'out' | 'in';

KEYWORD:
	'pushed' | 'released' | 'on' | 'off';

VAR_TYPE:
	'bool' | 'int' | 'long' | 'float' | 'double' | 'char' | 'string';

NEGATE_OP:
	'!';

LOGICAL_OP:
	'||' | '&&';

COMPARE_OP:
	'==' | '!=' | '<' | '>' | '<=' | '>=';

MATH_OP:
	'+' | '-' | '/' | '*' | '^';

fragment WIFI:
	'wifi';

terminal BOOL returns ecore::EBoolean:
	'true' | 'false';

terminal FLOAT returns ecore::EFloat:
	INT '.' INT;

terminal INT returns ecore::EInt: // Default (Terminals grammar)
	('0'..'9')+;

terminal CHAR returns ecore::EChar:
	"'" ('a'..'z' | 'A'..'Z') "'";

terminal STRING returns ecore::EString:
	'"' ('\\' . | !('\\' | '"'))* '"';

terminal ID:
	'^'? ('a'..'z' | 'A'..'Z' | '_') ('a'..'z' | 'A'..'Z' | '_' | '0'..'9')*;

terminal SL_COMMENT:
	'#' !('\n' | '\r')*;

terminal WS:
	(' ' | '\t' | '\r' | '\n')+;

terminal ANY_OTHER:
	.;
