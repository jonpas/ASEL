grammar org.jonpas.asel.ASEL hidden(WS, SL_COMMENT)

generate asel "http://www.jonpas.org/asel/ASEL"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

// Base program
Model:
	use+=Use* init=Init? prepare=Prepare? run=Run?;

Init:
	{Init} 'init' '{' code+=(InitCode | PreProcInit)* '}';

Prepare:
	{Prepare} 'prepare' '{' code+=(RunCode | PreProcRun)* '}';

Run:
	{Run} 'run' '{' code+=(RunCode | PreProcRun)* '}';

Use:
	'use' name=ID;

	// Code structure
InitCode:
	pin=InitPin | variable=InitVar | class=InitClass | pageHandle=PageHandler | InitWiFi | AnyCode;

RunCode:
	ModePin | VarAssign | {RunCode} WIFI | RETURN | AnyCode;

AnyCode:
	FuncCall | MethodCall | Logical | Loop;

	// init
InitPin:
	'pin' name=ID '=' value=INT;

InitVar:
	type=VAR_TYPE name=ID (single=InitSingle? | array=InitArray);

InitSingle:
	'=' sign=SIGN? value=Param;

InitArray:
	data=ArrayAccess ('=' '{' value+=Param* '}')?;

InitClass:
	'class' name=ID '=' className=ID '(' param+=Param* ')';

PageHandler:
	'pagehandle' name=ID '{' code+=PageHandlerCode* '}';

PageHandlerCode:
	arg=HandleArg | code=(RunCode | PreProcRun);

HandleArg:
	'handle' name=STRING '{' code+=PageHandlerCode* '}';

RETURN:
	'->' name=STRING;

InitWiFi:
	WIFI ssid=STRING password=STRING pageFile=STRING styleFile=STRING '{' buttonLinks+=WiFiLink* '}';

WiFiLink:
	button=STRING '->' name=[PageHandler];

	// run
ModePin:
	mode=PIN_MODE name=[InitPin];

VarAssign:
	name=ID arrayLength=ArrayAccess? MATH_OP? '=' sign=SIGN? value=Param;

	// any
FuncCall:
	name=ID '(' param+=Param* ')';

MethodCall:
	name=ID '.' method=ID '(' param+=Param* ')';

PreProcInit:
	PreProcRaw | PreProcIfdefInit;

PreProcRun:
	PreProcRaw | PreProcIfdefRun;

PreProcRaw:
	'raw' raw=STRING;

PreProcIfdefInit:
	'ifdef' cond=Condition '{' code+=InitCode* '}';

PreProcIfdefRun:
	'ifdef' cond=Condition '{' code+=RunCode* '}';

Param:
	value=VarValue | func=FuncCall | method=MethodCall | expr=MathExpr;

	// Expressions
Logical:
	LogicalIf;

LogicalIf:
	'if' cond=Condition '{' code+=(RunCode | PreProcRun)* '}' else+=LogicalElse*;

LogicalElse:
	{LogicalElse} 'else' ('if' nestedCond=Condition)? '{' code+=(RunCode | PreProcRun)* '}';

Loop:
	LoopFor | LoopWhile;

LoopFor:
	'for' name=ID 'in' ForType '->' '='? ForType '{' code+=(RunCode | PreProcRun)* '}';

LoopWhile:
	'while' cond=Condition '{' code+=(RunCode | PreProcRun)* '}';

Condition:
	NEGATE_OP? type=ConditionType (LOGICAL_OP sign+=NEGATE_OP? cond+=ConditionType)*;

ConditionType:
	val=VarValue | comp=Comparison | func=FuncCall | method=MethodCall;

Comparison:
	val1=VarValue comp=COMPARE_OP val2=VarValue;

MathExpr:
	val1=VarValue subExpr+=MathSubExpr+;

MathSubExpr:
	MATH_OP val2=VarValue;

	// General
VarValue:
	value=(ValueBool | ValueInt /* LONG same as INT */ | ValueFloat /* DOUBLE same as FLOAT */)
	| char=CHAR | string=STRING | keyword=KEYWORD | name=ID | name=ID data=ArrayAccess;

ValueBool:
	value=BOOL;

ValueInt:
	value=INT;

ValueFloat:
	value=FLOAT;

ForType:
	INT /* LONG same as INT */ | FLOAT /* DOUBLE same as FLOAT */ | ID;

ArrayAccess:
	'[' (length=INT | variable=[InitVar]) ']';

PIN_MODE:
	'out' | 'in';

KEYWORD:
	'pushed' | 'released' | 'on' | 'off';

VAR_TYPE:
	'bool' | 'int' | 'long' | 'float' | 'double' | 'char' | 'string';

SIGN returns ecore::EString:
	'-' | '+' | NEGATE_OP;

NEGATE_OP:
	'!';

LOGICAL_OP:
	'||' | '&&';

COMPARE_OP:
	'==' | '!=' | '<' | '>' | '<=' | '>=';

MATH_OP:
	'+' | '-' | '/' | '*' | '^';

fragment WIFI:
	'wifi';

terminal BOOL returns ecore::EBoolean:
	'true' | 'false';

terminal FLOAT returns ecore::EFloat:
	INT '.' INT;

terminal INT returns ecore::EInt: // Default (Terminals grammar)
	('0'..'9')+;

terminal CHAR returns ecore::EString:
	"'" ('a'..'z' | 'A'..'Z') "'";

terminal STRING returns ecore::EString:
	'"' ('\\' . | !('\\' | '"'))* '"';

terminal ID:
	'^'? ('a'..'z' | 'A'..'Z' | '_') ('a'..'z' | 'A'..'Z' | '_' | '0'..'9')*;

terminal SL_COMMENT:
	'#' !('\n' | '\r')*;

terminal WS:
	(' ' | '\t' | '\r' | '\n')+;
